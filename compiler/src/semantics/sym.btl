module sym;

import "../support/utils";
import "core";
import "type";

func find_record_field_by_name(sym: *Sym, name: *Char): Int {
    var fields = (sym as *RecordSym).fields;
    if (!fields) {
        return -1;
    }
    for (var i = 0; i < list_len(fields); i += 1) {
        var field = list_get(fields, i) as *RecordField;
        if (str_eq(field.name, name)) {
            return i;
        }
    }
    return -1;
}

func find_record_field_by_type(sym: *Sym, type: *Type): Int {
    var fields = (sym as *RecordSym).fields;
    if (!fields) {
        return -1;
    }
    for (var i = 0; i < list_len(fields); i += 1) {
        var field = list_get(fields, i) as *RecordField;
        if (type_eq(field.type, type)) {
            return i;
        }
    }
    return -1;
}

func find_param_by_name(sym: *FuncSym, name: *Int8): Int {
    for (var i = 0; i < list_len(sym.params); i += 1) {
        var param = list_get(sym.params, i) as *FuncParam;
        if (str_eq(param.name, name)) {
            return i;
        }
    }
    return -1;
}

func field_offset(sym: *RecordSym, field_index: Int): Int {
    if (sym.is_union) {
        return 0;
    }

    var unpadded_size = 0;
    var offset = 0;

    for (var i = 0; i <= field_index; i += 1) {
        var field = list_get(sym.fields, i) as *RecordField;
        var layout = type_layout(field.type);
        offset = align_up(unpadded_size, layout.align);
        unpadded_size = offset + layout.size;
    }

    return offset;
}
