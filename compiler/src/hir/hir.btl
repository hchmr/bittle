module hir;

import "../semantics/core";
import "../semantics/type";
import "../support/libc";
import "../support/utils";

struct HirTemp {
    type: *Type,
    slot_id: Int,
}

enum HirOpKind {
    HirOp_Or,
    HirOp_Xor,
    HirOp_And,
    HirOp_Shl,
    HirOp_Shr,
    HirOp_Eq,
    HirOp_Ne,
    HirOp_Lt,
    HirOp_Le,
    HirOp_Gt,
    HirOp_Ge,
    HirOp_Add,
    HirOp_Sub,
    HirOp_Mul,
    HirOp_Div,
    HirOp_Rem,
}

enum HirExprKind {
    // Composition primitives
    HirExpr_Skip,
    HirExpr_Seq,

    // Constants
    HirExpr_Int,
    HirExpr_Str,

    // Variables
    HirExpr_Var,
    HirExpr_Temp,

    // Control flow
    HirExpr_Cond,
    HirExpr_Loop,
    HirExpr_Jump,
    HirExpr_Return,

    // Expressions
    HirExpr_BinaryOp,
    HirExpr_Call,
    HirExpr_Member,
    HirExpr_Index,
    HirExpr_Deref,
    HirExpr_Addr,
    HirExpr_Assign,
    HirExpr_Cast,

    // Special
    HirExpr_Unreachable,
}

struct HirExpr {
    kind: HirExprKind,
    type: *Type,
    pos: Pos,
}

struct HirSkipExpr: HirExpr {
    kind = HirExpr_Skip,
}

struct HirSeqExpr: HirExpr {
    kind = HirExpr_Seq,
    first: *mut HirExpr,
    second: *mut HirExpr,
}

struct HirIntExpr: HirExpr {
    kind = HirExpr_Int,
    value: Int,
}

struct HirStrExpr: HirExpr {
    kind = HirExpr_Str,
    value: *StringBuffer,
}

struct HirVarExpr: HirExpr {
    kind = HirExpr_Var,
    sym: *mut Sym,
}

struct HirTempExpr: HirExpr {
    kind = HirExpr_Temp,
    temp: *mut HirTemp,
}

struct HirCondExpr: HirExpr {
    kind = HirExpr_Cond,
    cond: *mut HirExpr,
    then_expr: *mut HirExpr,
    else_expr: *mut HirExpr,
}

struct HirLoopExpr: HirExpr {
    kind = HirExpr_Loop,
    cond: *mut HirExpr,
    body: *mut HirExpr,
    step: *mut HirExpr,
}

struct HirReturnExpr: HirExpr {
    kind = HirExpr_Return,
    expr: *mut HirExpr,
}

struct HirJumpExpr: HirExpr {
    kind = HirExpr_Jump,
    is_break: Bool,
}

struct HirAssignExpr: HirExpr {
    kind = HirExpr_Assign,
    dst: *mut HirExpr,
    src: *mut HirExpr,
}

struct HirBinaryOpExpr: HirExpr {
    kind = HirExpr_BinaryOp,
    op: HirOpKind,
    left: *mut HirExpr,
    right: *mut HirExpr,
}

struct HirCallExpr: HirExpr {
    kind = HirExpr_Call,
    callee: *mut FuncSym,
    args: *mut *mut HirExpr,
    n_args: Int,
}

struct HirMemberExpr: HirExpr {
    kind = HirExpr_Member,
    left: *mut HirExpr,
    name: *Char,
    field: *mut RecordField,
}

struct HirIndexExpr: HirExpr {
    kind = HirExpr_Index,
    indexee: *mut HirExpr,
    index: *mut HirExpr,
}

struct HirDerefExpr: HirExpr {
    kind = HirExpr_Deref,
    expr: *mut HirExpr,
}

struct HirAddrExpr: HirExpr {
    kind = HirExpr_Addr,
    expr: *mut HirExpr,
}

struct HirCastExpr: HirExpr {
    kind = HirExpr_Cast,
    expr: *mut HirExpr,
}

struct HirUnreachableExpr: HirExpr {
    kind = HirExpr_Unreachable,
}

func hir_is_lvalue(e: *HirExpr): Bool {
    return e.kind is (
        HirExpr_Var
        | HirExpr_Temp
        | HirExpr_Member
        | HirExpr_Index
        | HirExpr_Deref
    );
}

var hir_indent: Int;

func print_spaces(n: Int) {
    for (var i = 0; i < n; i += 1) {
        fprintf(stderr, "  ");
    }
}

func begin_block(name: *Char) {
    print_spaces(hir_indent);
    fprintf(stderr, "%s {\n", name);
    hir_indent += 1;
}

func end_block() {
    print_spaces(hir_indent - 1);
    hir_indent -= 1;
    fprintf(stderr, "}\n");
}

func begin_child(name: *Char) {
    print_spaces(hir_indent);
    if (name != null) {
        fprintf(stderr, "%s:\n", name);
    }
    hir_indent += 1;
}

func end_child() {
    hir_indent -= 1;
}

func begin_inline_child(name: *Char) {
    print_spaces(hir_indent);
    if (name != null) {
        fprintf(stderr, "%s: ", name);
    }
}

func hir_dump_type(t: *Type) {
    print_spaces(hir_indent);
    var pretty = pretty_type(t);
    fprintf(stderr, "%s\n", pretty);
    free(pretty);
}

func hir_dump_child(name: *Char, e: *HirExpr) {
    begin_child(name);
    if (!e) {
        begin_inline_child("<<<null>>>");
    } else {
        match (e.kind) {
            case HirExpr_Skip: {
                begin_block("Skip");
                end_block();
            }
            case HirExpr_Seq: {
                var e = e as *HirSeqExpr;
                begin_block("Seq");
                hir_dump_child("first", e.first);
                hir_dump_child("second", e.second);
                end_block();
            }
            case HirExpr_Int: {
                var e = e as *HirIntExpr;
                begin_inline_child("int");
                fprintf(stderr, "%d\n", e.value);
            }
            case HirExpr_Str: {
                var e = e as *HirStrExpr;
                begin_inline_child("str");
                fprintf(stderr, "\"%s\"\n", sb_cstr(e.value));
            }
            case HirExpr_Var: {
                var e = e as *HirVarExpr;
                begin_inline_child("var");
                fprintf(stderr, "%s\n", e.sym.name);
            }
            case HirExpr_Temp: {
                var e = e as *HirTempExpr;
                begin_inline_child("temp");
                fprintf(stderr, "%d\n", e.temp.slot_id);
            }
            case HirExpr_Cond: {
                var e = e as *HirCondExpr;
                begin_block("Cond");
                hir_dump_child("cond", e.cond);
                hir_dump_child("then_expr", e.then_expr);
                hir_dump_child("else_expr", e.else_expr);
                end_block();
            }
            case HirExpr_Loop: {
                var e = e as *HirLoopExpr;
                begin_block("Loop");
                hir_dump_child("body", e.body);
                hir_dump_child("step", e.step);
                end_block();
            }
            case HirExpr_Jump: {
                var e = e as *HirJumpExpr;
                begin_block(e.is_break ? "break" : "continue");
                end_block();
            }
            case HirExpr_Return: {
                var e = e as *HirReturnExpr;
                begin_block("Return");
                if (e.expr != null) {
                    hir_dump_child("expr", e.expr);
                }
                end_block();
            }
            case HirExpr_BinaryOp: {
                var e = e as *HirBinaryOpExpr;
                begin_block("Binop");
                hir_dump_child("left", e.left);
                begin_inline_child("op");
                fprintf(stderr, "%d\n", e.op);
                hir_dump_child("right", e.right);
                end_block();
            }
            case HirExpr_Call: {
                var e = e as *HirCallExpr;
                begin_block("Call");
                begin_inline_child("name");
                fprintf(stderr, "\"%s\"\n", e.callee.name);
                begin_child("args");
                for (var i = 0; i < e.n_args; i += 1) {
                    hir_dump_child(name: null, e: e.args[i]);
                }
                end_child();
                end_block();
            }
            case HirExpr_Member: {
                var e = e as *HirMemberExpr;
                begin_block("Member");
                hir_dump_child("left", e.left);
                begin_inline_child("field");
                fprintf(stderr, "%s\n", e.name);
                end_block();
            }
            case HirExpr_Index: {
                var e = e as *HirIndexExpr;
                begin_block("Index");
                hir_dump_child("indexee", e.indexee);
                hir_dump_child("index", e.index);
                end_block();
            }
            case HirExpr_Deref: {
                var e = e as *HirDerefExpr;
                begin_block("Deref");
                hir_dump_child("expr", e.expr);
                end_block();
            }
            case HirExpr_Addr: {
                var e = e as *HirAddrExpr;
                begin_block("Addr");
                hir_dump_child("expr", e.expr);
                end_block();
            }
            case HirExpr_Assign: {
                var e = e as *HirAssignExpr;
                begin_block("Assign");
                hir_dump_child("dst", e.dst);
                hir_dump_child("src", e.src);
                end_block();
            }
            case HirExpr_Cast: {
                var e = e as *HirCastExpr;
                begin_block("Cast");
                hir_dump_child("expr", e.expr);
                begin_child("type");
                hir_dump_type(e.type);
                end_child();
                end_block();
            }
            case HirExpr_Unreachable: {
                begin_block("Unreachable");
                end_block();
            }
            case other @ _: {
                unreachable_enum_case("hir_dump_", other);
            }
        }
    }
    end_child();
}

func hir_dump(e: *HirExpr): Void {
    fprintf(stderr, "%s:%d:%d:\n", e.pos.file, e.pos.row, e.pos.col);
    hir_dump_child(name: null, e: e);
}
